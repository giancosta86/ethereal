use ./command
use ./fs
use ./tracer

fn run-tests-for-tracer { |tracer stream|
  fn assert-tracer-data { |block expected-data|
    command:capture &stream=$stream $block |
      put (all)[data] |
      should-be $expected-data
  }

  >> 'echo' {
    assert-tracer-data {
      $tracer[echo] YOGI
    } [
      YOGI
    ]
  }

  >> 'print' {
    assert-tracer-data {
      $tracer[print] YOGI
    } [
      YOGI
    ]
  }

  >> 'printf' {
    var base = Dodo
    var value = 90

    assert-tracer-data {
      $tracer[printf] &newline $base': %s' $value
    } [
      $base': '$value
    ]
  }

  >> 'pprint' {
    assert-tracer-data {
      $tracer[pprint] [ A B C ]
    } [
        '['
        ' A'
        ' B'
        ' C'
        ']'
      ]
  }

  >> 'inspect' {
    assert-tracer-data {
      $tracer[inspect] Map [ &x=90 &y=92 ]
    } [
      'ðŸ”Ž Map: ['
      " &x=\t90"
      " &y=\t92"
      ']'
    ]
  }

  >> 'inspect-input-map' {
    assert-tracer-data {
      $tracer[inspect-input-map] [&a=90 &b=dodo]
    } [
      'ðŸ“¥ Input map: ['
      " &a=\t90"
      " &b=\tdodo"
      ']'
    ]
  }

  >> 'section' {
    >> 'with string' {
      assert-tracer-data {
        $tracer[section] &emoji=ðŸ“š 'Description' "Some\ntext"
      } [
        'ðŸ“š Description:'
        Some
        text
        ðŸ“šðŸ“šðŸ“š
      ]
    }

    >> 'with block' {
      assert-tracer-data {
        $tracer[section] &emoji=ðŸ“š 'Description' {
          echo Alpha
          echo Beta
          $tracer[inspect] Gamma (num 92)
        }
      } [
        'ðŸ“š Description:'
        Alpha
        Beta
        'ðŸ”Ž Gamma: (num 92)'
        ðŸ“šðŸ“šðŸ“š
      ]
    }
  }
}

>> 'Tracer' {
  var scenarios = [
    [
      &stream=out
      &writer=$tracer:out-writer
    ]
    [
      &stream=err
      &writer=$tracer:err-writer
    ]
  ]
  all $scenarios | each { |scenario|
    var stream = $scenario[stream]
    var writer = $scenario[writer]

    >> 'when writing to '$stream {
      >> 'when based on a variable' {
        var tracer = (tracer:create $true &writer=$writer)

        run-tests-for-tracer $tracer $stream
      }

      >> 'when based on a block' {
        var tracer = (tracer:create { put $true } &writer=$writer)

        run-tests-for-tracer $tracer $stream
      }
    }
  }

  >> 'when writing to a file' {
    fs:with-temp-file { |temp-path|
      var writer = (tracer:create-file-writer $temp-path)

      var tracer = (tracer:create $true &writer=$writer)

      $tracer[echo] Dodo
      $tracer[section] &emoji=ðŸ§­ 'Basic test' {
        $tracer[print] 'Hello, '
        $tracer[echo] world
        $tracer[pprint] [90 92]
      }

      from-lines < $temp-path |
        put [(all)] |
        should-be [
          Dodo
         'ðŸ§­ Basic test:'
         'Hello, world'
         '['
         ' 90'
         ' 92'
         ']'
         ðŸ§­ðŸ§­ðŸ§­
        ]
    }
  }
}